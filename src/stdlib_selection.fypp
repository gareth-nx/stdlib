#:include "common.fypp"
! Specify kinds/types for the input array in select and arg_select
#:set ARRAY_KINDS_TYPES_OUTPUT = list(zip(INT_KINDS, INT_TYPES, INT_TYPES)) + list(zip(REAL_KINDS, REAL_TYPES, REAL_TYPES)) + [('character', 'character(len=*)', 'character(len=len(a))')] + [('string_type', 'type(string_type)', 'type(string_type)')]
! The index arrays are of all INT_KINDS_TYPES

module stdlib_selection
!
! This code was modified from the "Coretran" implementation "quickSelect" by
! Leon Foks, https://github.com/leonfoks/coretran/tree/master/src/sorting
!
! Leon Foks gave permission to be release this code under stdlib's MIT license.
! (https://github.com/fortran-lang/stdlib/pull/500#commitcomment-57418593)
!

use stdlib_kinds, only: int8, int16, int32, int64, sp, dp, qp
use stdlib_string_type

implicit none

private

public select, arg_select

interface select
    !! version: experimental
    !! ([Specification](..//page/specs/stdlib_selection.html#select-find-the-k-th-smallest-value-in-an-input-array))

  #:for arraykind, arraytype, arrayouttype in ARRAY_KINDS_TYPES_OUTPUT
    #:for intkind, inttype in INT_KINDS_TYPES
      #:set name = rname("select", 1, arraytype, arraykind, intkind)
      module procedure ${name}$
    #:endfor
  #:endfor
end interface

interface arg_select
    !! version: experimental
    !! ([Specification](..//page/specs/stdlib_selection.html#arg_select-find-the-index-of-the-k-th-smallest-value-in-an-input-array))
  #:for arraykind, arraytype, arrayouttype in ARRAY_KINDS_TYPES_OUTPUT
    #:for intkind, inttype in INT_KINDS_TYPES
      #:set name = rname("arg_select", 1, arraytype, arraykind, intkind)
      module procedure ${name}$
    #:endfor
  #:endfor
end interface

contains

  #:for arraykind, arraytype, arrayouttype in ARRAY_KINDS_TYPES_OUTPUT
    #:for intkind, inttype in INT_KINDS_TYPES
      #:set name = rname("select", 1, arraytype, arraykind, intkind)
      subroutine ${name}$(a, k, kth_smallest, left, right)
          !! select - select the k-th smallest entry in a(:).
          !!
          !! Partly derived from the "Coretran" implementation of 
          !! quickSelect by Leon Foks, https://github.com/leonfoks/coretran
          !!
          ${arraytype}$, intent(inout) :: a(:)
              !! Array in which we seek the k-th smallest entry.
              !! On output it will be partially sorted such that
              !! `all(a(1:(k-1)) <= a(k)) .and. all(a(k) <= a((k+1):size(a)))`.
          ${inttype}$, intent(in) :: k
              !! We want the k-th smallest entry. E.G. `k=1` leads to
              !! `kth_smallest=min(a)`, and `k=size(a)` leads to
              !! `kth_smallest=max(a)`
          ${arrayouttype}$, intent(out) :: kth_smallest
              !! On output contains the k-th smallest value of `a(:)`
          ${inttype}$, intent(in), optional :: left, right
              !! If we know that:
              !!    the k-th smallest entry of `a` is in `a(left:right)`
              !! and also that:
              !!    `maxval(a(1:(left-1))) <= minval(a(left:right))`
              !! and:
              !!    `maxval(a(left:right))) <= minval(a((right+1):size(a)))`
              !! then one or both bounds can be specified to narrow the search.
              !! The constraints are available if we have previously called the
              !! subroutine with different `k` (because of how `a(:)` becomes
              !! partially sorted, see documentation for `a(:)`).

          ${inttype}$ :: l, r, mid, iPivot
          integer, parameter :: ip = ${intkind}$

          l = 1_ip
          if(present(left)) l = left
          r = size(a, kind=ip)
          if(present(right)) r = right

          if(k < 1_ip .or. k > size(a, kind=ip) .or. l > r .or. l < 1_ip .or. &
              r > size(a, kind=ip)) then
              error stop "select must have 1 <= k <= size(a), and 1 <= left <= right <= size(a)";
          end if

          do while(.true.)
              mid = (l+r)/2_ip ! Deliberate integer division

              call medianOf3(a, l, mid, r)
              call swap(a(l), a(mid))
              call partition(a, l, r, iPivot)

              if (iPivot < k) then
                l = iPivot + 1_ip
              elseif (iPivot > k) then
                r = iPivot - 1_ip
              elseif (iPivot == k) then
                kth_smallest = a(k)
                return
              end if
          end do

          contains
              subroutine swap(a, b)
                  ${arraytype}$, intent(inout) :: a, b
                  ${arrayouttype}$ :: tmp
                  tmp = a; a = b; b = tmp
              end subroutine

              subroutine medianOf3(a, left, mid, right)
                  ${arraytype}$, intent(inout) :: a(:)
                  ${inttype}$, intent(in) :: left, mid, right 
                  if(a(right) < a(left)) call swap(a(right), a(left))
                  if(a(mid)   < a(left)) call swap(a(mid)  , a(left))
                  if(a(right) < a(mid) ) call swap(a(mid)  , a(right))
              end subroutine

              subroutine partition(a,left,right,iPivot)
                  ${arraytype}$, intent(inout) :: a(:)
                  ${inttype}$, intent(in) :: left, right
                  ${inttype}$, intent(out) :: iPivot 
                  ${inttype}$ :: lo,hi
                  ${arrayouttype}$ :: pivot

                  pivot = a(left)
                  lo = left
                  hi=right
                  do while (lo <= hi)
                    do while (a(hi) > pivot)
                      hi=hi-1_ip
                    end do
                    do while (lo <= hi )
                       if(a(lo) > pivot) exit
                      lo=lo+1_ip
                    end do
                    if (lo <= hi) then
                      call swap(a(lo),a(hi))
                      lo=lo+1_ip
                      hi=hi-1_ip
                    end if
                  end do
                  call swap(a(left),a(hi))
                  iPivot=hi
              end subroutine
      end subroutine
    #:endfor
  #:endfor


  #:for arraykind, arraytype, arrayouttype in ARRAY_KINDS_TYPES_OUTPUT
    #:for intkind, inttype in INT_KINDS_TYPES
      #:set name = rname("arg_select", 1, arraytype, arraykind, intkind)
      subroutine ${name}$(a, indx, k, kth_smallest, left, right)
          !! arg_select - find the index of the k-th smallest entry in `a(:)`
          !!
          !! Partly derived from the "Coretran" implementation of 
          !! quickSelect by Leon Foks, https://github.com/leonfoks/coretran
          !!
          ${arraytype}$, intent(in) :: a(:)
              !! Array in which we seek the k-th smallest entry.
          ${inttype}$, intent(inout) :: indx(:)
              !! Array of indices into `a(:)`. Must contain each integer
              !! from `1:size(a)` exactly once. On output it will be partially
              !! sorted such that
              !! `all( a(indx(1:(k-1)))) <= a(indx(k)) ) .AND.
              !!  all( a(indx(k))  <= a(indx( (k+1):size(a) )) )`.
          ${inttype}$, intent(in) :: k
              !! We want index of the k-th smallest entry. E.G. `k=1` leads to
              !! `a(kth_smallest) = min(a)`, and `k=size(a)` leads to
              !! `a(kth_smallest) = max(a)`
          ${inttype}$, intent(out) :: kth_smallest
              !! On output contains the index with the k-th smallest value of `a(:)`
          ${inttype}$, intent(in), optional :: left, right
              !! If we know that:
              !!  the k-th smallest entry of `a` is in `a(indx(left:right))`
              !! and also that:
              !!  `maxval(a(indx(1:(left-1)))) <= minval(a(indx(left:right)))`
              !! and:
              !!  `maxval(a(indx(left:right))) <= minval(a(indx((right+1):size(a))))`
              !! then one or both bounds can be specified to reduce the search
              !! time. These constraints are available if we have previously
              !! called the subroutine with a different `k` (due to the way that
              !! `indx(:)` becomes partially sorted, see documentation for `indx(:)`).

          ${inttype}$ :: l, r, mid, iPivot
          integer, parameter :: ip = ${intkind}$

          l = 1_ip
          if(present(left)) l = left
          r = size(a, kind=ip)
          if(present(right)) r = right

          if(size(a) /= size(indx)) then
              error stop "arg_select must have size(a) == size(indx)"
          end if

          if(k < 1_ip .or. k > size(a, kind=ip) .or. l > r .or. l < 1_ip .or. &
              r > size(a, kind=ip)) then
              error stop "arg_select must have 1 <= k <= size(a), and 1 <= left <= right <= size(a)";
          end if

          do while(.true.)
              mid = (l+r)/2_ip ! Deliberate integer division

              call arg_medianOf3(a, indx, l, mid, r)
              call swap(indx(l), indx(mid))
              call arg_partition(a, indx, l, r, iPivot)

              if (iPivot < k) then
                l = iPivot + 1_ip
              elseif (iPivot > k) then
                r = iPivot - 1_ip
              elseif (iPivot == k) then
                kth_smallest = indx(k)
                return
              end if
          end do

          contains
              subroutine swap(a, b)
                  ${inttype}$, intent(inout) :: a, b
                  ${inttype}$ :: tmp
                  tmp = a; a = b; b = tmp
              end subroutine

              subroutine arg_medianOf3(a, indx, left, mid, right)
                  ${arrayouttype}$, intent(in) :: a(:)
                  ${inttype}$, intent(inout) :: indx(:)
                  ${inttype}$, intent(in) :: left, mid, right 
                  if(a(indx(right)) < a(indx(left))) call swap(indx(right), indx(left))
                  if(a(indx(mid))   < a(indx(left))) call swap(indx(mid)  , indx(left))
                  if(a(indx(right)) < a(indx(mid)) ) call swap(indx(mid)  , indx(right))
              end subroutine

              subroutine arg_partition(a, indx, left,right,iPivot)
                  ${arraytype}$, intent(in) :: a(:)
                  ${inttype}$, intent(inout) :: indx(:)
                  ${inttype}$, intent(in) :: left, right
                  ${inttype}$, intent(out) :: iPivot 
                  ${inttype}$ :: lo,hi
                  ${arrayouttype}$ :: pivot

                  pivot = a(indx(left))
                  lo = left
                  hi = right
                  do while (lo <= hi)
                    do while (a(indx(hi)) > pivot)
                      hi=hi-1_ip
                    end do
                    do while (lo <= hi )
                      if(a(indx(lo)) > pivot) exit
                      lo=lo+1_ip
                    end do
                    if (lo <= hi) then
                      call swap(indx(lo),indx(hi))
                      lo=lo+1_ip
                      hi=hi-1_ip
                    end if
                  end do
                  call swap(indx(left),indx(hi))
                  iPivot=hi
              end subroutine
      end subroutine
    #:endfor
  #:endfor

end module


